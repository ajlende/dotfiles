# ajlende's prompt (aprompt)

# ANSI color escape codes with my colorscheme colors commented
APROMPT_COLOR_BLACK='%{\e[30m%}'   # black
APROMPT_COLOR_WHITE='%{\e[37m%}'   # white
APROMPT_COLOR_RED='%{\e[31m%}'     # red
APROMPT_COLOR_BLUE='%{\e[34m%}'    # orange
APROMPT_COLOR_YELLOW='%{\e[33m%}'  # yellow
APROMPT_COLOR_GREEN='%{\e[32m%}'   # green
APROMPT_COLOR_CYAN='%{\e[36m%}'    # blue
APROMPT_COLOR_MAGENTA='%{\e[35m%}' # purple
APROMPT_COLOR_RESET='%{\e[0m%}'    # reset

# Prompt colors
APROMPT_COLORS_USER_HOST=$APROMPT_COLOR_YELLOW
APROMPT_COLORS_PWD=$APROMPT_COLOR_CYAN
APROMPT_COLORS_RETURN_TRUE=$APROMPT_COLOR_MAGENTA
APROMPT_COLORS_RETURN_FALSE=$APROMPT_COLOR_RED
APROMPT_COLORS_VCS_REPO=$APROMPT_COLOR_CYAN
APROMPT_COLORS_VCS_DEFAULT=$APROMPT_COLOR_MAGENTA
APROMPT_COLORS_VCS_UNSTAGED=$APROMPT_COLOR_RED
APROMPT_COLORS_VCS_STAGED=$APROMPT_COLOR_GREEN
APROMPT_COLORS_BG_JOBS=$APROMPT_COLOR_BLUE
APROMPT_COLORS_VCS_ACTION=$APROMPT_COLOR_RED
APROMPT_COLORS_VCS_STACK=$APROMPT_COLOR_BLACK
APROMPT_COLORS_VCS_MESSAGE=$APROMPT_COLOR_WHITE

# Prompt symbols for insert/main and command mode
APROMPT_VIINS='❯'
APROMPT_VICMD='❮'

# Set symbol and redraw prompt when switching modes
APROMPT_SYMBOL="$APROMPT_VIINS"
function zle-line-init zle-keymap-select {
	APROMPT_SYMBOL="${${KEYMAP/vicmd/$APROMPT_VICMD}/(main|viins)/$APROMPT_VIINS}"
	zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# Set window title to git repo root or basename
aprompt_window_title() {
	local title=""
	local repo=$(git rev-parse --show-toplevel 2>/dev/null)
	if [[ -n "${repo}" ]]; then
		title=$(basename "${repo}")
	elif [[ "$PWD" = "$HOME" ]]; then
		title="~"
	else
		title=`basename "$PWD"`
	fi
	echo -ne "\e]0;${title}\a"
}
aprompt_window_title
chpwd_functions+=( aprompt_window_title )

# Enable native zsh git/jj support populating $vcs_info_msg_0_
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
fpath+="$HOME/.zsh/functions/vcs_info/backends"
zstyle ':vcs_info:*' enable jj git
zstyle ':vcs_info:*' max-exports 1
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "$APROMPT_COLORS_VCS_STAGED"
zstyle ':vcs_info:*' unstagedstr "$APROMPT_COLORS_VCS_UNSTAGED"
zstyle ':vcs_info:*' formats       "$APROMPT_COLORS_VCS_REPO%r$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_DEFAULT%u%c%b$APROMPT_COLOR_RESET $APROMPT_COLORS_PWD%S$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_STACK%Q$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_MESSAGE%m$APROMPT_COLOR_RESET"
zstyle ':vcs_info:*' actionformats "$APROMPT_COLORS_VCS_REPO%r$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_DEFAULT%u%c%b$APROMPT_COLOR_RESET $APROMPT_COLORS_PWD%S$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_STACK%Q$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_ACTION%a$APROMPT_COLOR_RESET $APROMPT_COLORS_VCS_MESSAGE%m$APROMPT_COLOR_RESET"
zstyle ':vcs_info:jj*+set-message:*' hooks jj-stack-info
+vi-jj-stack-info() {
    local current_id
    local -a stack_commits
	local -i count position
    current_id=$(${vcs_comm[cmd]} log --no-graph --ignore-working-copy -r '@' -T 'change_id' 2>/dev/null)
    stack_commits=(${(f)"$(${vcs_comm[cmd]} log --no-graph --ignore-working-copy \
		-r 'coalesce(immutable(), root())..present(@) | descendants(present(@))' \
		-T 'change_id ++ "\n"' 2>/dev/null)"})
    count=${#stack_commits}
    position=${stack_commits[(i)${current_id}]}
    position=$((count - position + 1))
    hook_com[quilt]="[${position}/${count}]"
	# hook_com[quilt] populates %Q
}
# zstyle ':vcs_info:*' stagedstr "S"
# zstyle ':vcs_info:*' unstagedstr "U"
# zstyle ':vcs_info:*' formats       "vcs(%s) commit(%i) unstaged($APROMPT_COLORS_VCS_UNSTAGED%u$APROMPT_COLOR_RESET) staged($APROMPT_COLORS_VCS_STAGED%c$APROMPT_COLOR_RESET) branch($APROMPT_COLORS_VCS_DEFAULT%b$APROMPT_COLOR_RESET) repo($APROMPT_COLORS_VCS_REPO%r$APROMPT_COLOR_RESET) subdir($APROMPT_COLORS_PWD%S$APROMPT_COLOR_RESET) quilt($APROMPT_COLORS_VCS_STACK%Q$APROMPT_COLOR_RESET) misc($APROMPT_COLORS_VCS_MESSAGE%m$APROMPT_COLOR_RESET)"
# zstyle ':vcs_info:*' actionformats "vcs(%s) commit(%i) unstaged($APROMPT_COLORS_VCS_UNSTAGED%u$APROMPT_COLOR_RESET) staged($APROMPT_COLORS_VCS_STAGED%c$APROMPT_COLOR_RESET) branch($APROMPT_COLORS_VCS_DEFAULT%b$APROMPT_COLOR_RESET) repo($APROMPT_COLORS_VCS_REPO%r$APROMPT_COLOR_RESET) subdir($APROMPT_COLORS_PWD%S$APROMPT_COLOR_RESET) quilt($APROMPT_COLORS_VCS_STACK%Q$APROMPT_COLOR_RESET) action($APROMPT_COLORS_VCS_ACTION%a$APROMPT_COLOR_RESET) misc($APROMPT_COLORS_VCS_MESSAGE%m$APROMPT_COLOR_RESET)"

# Enable redrawing of prompt variables for vcs_info and zle keymap (vi mode)
setopt prompt_subst

# Version control info or just pwd if not in a repo
aprompt_vcs_pwd() {
	if [[ -n "${vcs_info_msg_0_}" ]]; then
		echo "${vcs_info_msg_0_}"
	else
		echo "$APROMPT_COLORS_PWD%~$APROMPT_COLOR_RESET"
	fi
}

# Background jobs if any
aprompt_bg_jobs() {
	echo "%(1j.$APROMPT_COLORS_BG_JOBS↓%j$APROMPT_COLOR_RESET .)"
}

# User or user@host if differnet from current user
aprompt_user_host() {
	local me=""
	if [[ -n $SSH_CONNECTION ]]; then
		me="%n@%m"
	elif [[ $LOGNAME != $USER || $USER = 'root' ]]; then
		me="%n"
	fi
	[[ -n $me ]] && echo "$APROMPT_COLORS_USER_HOST$me$APROMPT_COLOR_RESET:"
}

# Prompt symbol colored by return status
aprompt_symbol_return() {
	echo "%(?.$APROMPT_COLORS_RETURN_TRUE.$APROMPT_COLORS_RETURN_FALSE)$APROMPT_SYMBOL$APROMPT_COLOR_RESET "
}

# PS1 Prompt
PROMPT='
$(aprompt_vcs_pwd)
$(aprompt_bg_jobs)$(aprompt_user_host)$(aprompt_symbol_return)'
